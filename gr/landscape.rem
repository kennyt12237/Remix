
randomize

the-clouds : apply [create a cloud] 10 times
on standard (sky) paper
	draw the landscape

==================

create a cloud :
	base : random (std-height)
	across : random (std-width)
	radius1 : random 10 + 30
	radius2 : random 10 + 20
	distance : 10
	if (heads)
		temp : radius1
		radius1 : radius2
		radius2 : temp
	{
		base : base,
		radius1 : radius1,
		radius2 : radius2,
		x1 : (across - radius1) + distance,
		x2 : (across + radius2) - distance,
		y1 : base - radius1,
		y2 : base - radius2,
		draw : [draw cloud of (radius1) and (radius2) at {x1, y1} and {x2, y2}],
		move-x1 : [inc (x1)],
		move-x2 : [inc (x2)]
	}

draw cloud of (radius1) and (radius2) at (pos1) and (pos2) :
	draw (white) circle of (radius1) at (pos1)
	draw (white) circle of (radius2) at (pos2)
	draw (white) box from {pos1 [1], pos1 [2] + radius1} to (pos2)

draw the landscape :
	animate the clouds
	draw the middleground

animate the clouds :
	animate 10 times per sec
		clear layer 1
		no outline
		for each (cloud) in (the-clouds)
			cloud [move-x1]
			cloud [move-x2]
			cloud [draw]

draw the middleground :
	draw the back mountains
	draw the front mountains

draw the back mountains :
	draw on layer 2
	repeat 30 times
		draw a (coal) mountain of height (200) based at 200
	draw (coal) box from {0, std-height} to {std-width, std-height - 200}

draw the front mountains :
	draw on layer 3
	repeat 20 times
		if (random 2 = 1) [ no outline ] otherwise [ draw with (coal) pen ]
		draw a (gray) mountain of height (150) based at 150

draw a (colour) mountain of height (max-height) based at (level) :
	centre : random (std-width)
	height : random (max-height)
	peak : std-height - (height + level)
	width : random 100 + 250
	mountain : make shape of {
		{ centre, peak },
		{ centre + width รท 2, std-height - level },
		{ centre - width รท 2, std-height - level }
	} with size 1
	mountain [colour] : colour
	fill (mountain)
