Show a grid of everchanging squares.
====================================

make a square of (size) at (grid-pos) :
	half : size ÷ 2
	square : make shape of {
		{-1, -1},
		{ 1, -1},
		{ 1,  1},
		{-1,  1}
	} with size (half)
	x : (size × grid-pos [1]) - half
	y : (size × grid-pos [2]) - half
	square [position] : {x, y}

	create
		shape : square

		draw the () :
			fill (shape)

		change colour of () :
			shape [colour] : random colour

base-size : 20
columns   : std-width ÷ base-size
rows      : std-height ÷ base-size

squares : an empty list

for (rows) (down) and (columns) (across)
	append (make a square of (base-size) at {across, down}) to (squares)

on standard paper
	animate 1 time per sec
		clear layer 1
		no outline
		for each (square) in (squares)
			change colour of (square)
			draw the (square)
